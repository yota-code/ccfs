#!/usr/bin/env zsh

# this code must NOT rely on zprofile shortcuts


push_path() {

	pth=$1:A
	if [[ -d $pth ]]
	then
		echo "path += $pth"
		export PATH=${pth}/bin:$PATH
		export LD_LIBRARY_PATH=${pth}/lib:${pth}/lib64:$LD_LIBRARY_PATH

		rehash
	else
		echo "push_path($1): path don't exist"
	fi

}

if [[ -z $1 ]]
then
	echo "using default for $(hostname)"
	arg_dir="../root/hostname/$(hostname)"
else
	echo "using argument"
	arg_dir=$1
fi

specific_dir=${arg_dir:A}

if [[ -d $specific_dir && -f $specific_dir/compile_env ]]
then
	# in order to set the install mode, pass as first argument the ./root/plateform/$osys_$arch_$date directory

	platform_dir=${specific_dir:h}
	root_dir=${platform_dir:h}

	source_dir=${0:a:h}

	IFS='_' read -r osys arch date <<< ${specific_dir:t}

	export CCFS_compile_OSYS=$osys
	export CCFS_compile_ARCH=$arch
	export CCFS_compile_DATE=$date

	if [[ ${root_dir:t} != 'root' || -z $CCFS_compile_OSYS || -z $CCFS_compile_ARCH || -z $CCFS_compile_DATE ]]
	then
		echo "error: the platform dir must follow the convention ./root/plateform/$$osys_$$arch_$$date"
		return 1
	fi

	# a valid dir seem to have been provided, we use it to trigger the install mode

	if (( ${+ZPROFILE_path_OLD} ))
	then
		export PATH=$ZPROFILE_path_OLD
		echo "PATH=$PATH"
	fi

	if (( ${+ZPROFILE_ldlibrarypath_OLD} ))
	then
		export LD_LIBRARY_PATH=$ZPROFILE_ldlibrarypath_OLD
		echo "LD_LIBRARY_PATH=$LD_LIBRARY_PATH"
	fi

	rehash

	export CCFS_prefix_DIR=${specific_dir}

	export CCFS_archive_DIR=${root_dir:h}/archive

	export CCFS_custom_DIR=$source_dir/custom/${osys}_${arch}
	export CCFS_log_DIR=$specific_dir/log

	mkdir -p ${CCFS_log_DIR}

	export PATH=$source_dir/script:$PATH

	echo ">\n> install mode activated for: $osys _ $arch _ $date\n>"
	echo "CCFS_prefix_DIR=\e[33m$CCFS_prefix_DIR\e[0m"
	if [[ -f $CCFS_prefix_DIR/export_path ]]
	then
		source $CCFS_prefix_DIR/export_path
		echo "CFLAGS=\e[33m$CFLAGS\e[0m"
	fi
	if [[ -f $CCFS_prefix_DIR/reinstalled.log ]]
	then
		cat $CCFS_prefix_DIR/reinstalled.log
	fi
	if [[ -f $CCFS_prefix_DIR/compile_env ]]
	then
		source $CCFS_prefix_DIR/compile_env
		echo "CFLAGS=\e[33m$CFLAGS\e[0m"
	fi

	echo "----"

	push_path ${root_dir}/common
	push_path ${CCFS_prefix_DIR}

	typeset -T LD_LIBRARY_PATH ld_library_path

	typeset -U path
	typeset -U ld_library_path

	export LD_RUN_PATH=$LD_LIBRARY_PATH
	export LIBRARY_PATH=$LD_LIBRARY_PATH

	export PKG_CONFIG_PATH=${CCFS_prefix_DIR}/lib/pkgconfig

else

	echo "you must create a specific dir in ./root/platform and a link in ./root/hostname"

fi

